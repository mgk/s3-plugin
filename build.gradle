apply plugin: 'groovy'
apply plugin: 'maven-publish'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:${publishPluginVersion}"
  }
}

apply plugin: "com.gradle.plugin-publish"

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: awsSdkS3Version
}

repositories {
    jcenter()
}

if (System.env.TRAVIS == 'true') {
    def branch = System.env.TRAVIS_BRANCH.replace('/', '-')
    def isPullRequestBuild = System.env.TRAVIS_PULL_REQUEST != 'false'

    if (branch != 'master' || isPullRequestBuild) {
        version += "-${branch}-${System.env.TRAVIS_BUILD_NUMBER}"
    }
}

task versionFile {
    doLast {
        def f = file("${buildDir}/VERSION")
        f.parentFile.mkdirs()
        f.text = project.version
    }
}

jar.dependsOn versionFile

publishing {
    repositories {
        maven {
            url = uri("${rootProject.projectDir}/maven-repo")
        }
    }
}


gradlePlugin {
  website.set('https://github.com/mgk/s3-plugin/blob/master/README.md')
  vcsUrl.set('https://github.com/mgk/s3-plugin')

  plugins {
    create("s3Plugin") {
      id = 'com.github.mgk.gradle.s3'
      implementationClass = 'com.github.mgk.gradle.S3Plugin'
      description = 'S3 upload and download plugin'
      displayName = 'Gradle S3 plugin'
      tags.set(['aws', 's3'])
    }
  }
}

task checkExample(type: GradleBuild) {
    dir = 'example'
    tasks = ['check']
}
check.dependsOn checkExample
